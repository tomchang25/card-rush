extends Node2D

const ARC_POINTS = 8

@onready var area_2d: Area2D = $Area2D
@onready var card_arc: Line2D = %CardArc

var current_card: CardUI
var targeting: bool = false

func _ready() -> void:
    Events.card_aim_started.connect(_on_card_aim_started)
    Events.card_aim_ended.connect(_on_card_aim_ended)

func _process(_delta: float) -> void:
    if not targeting:
        return

    area_2d.position = get_local_mouse_position()
    card_arc.points = _get_arc_points()

func _ease_out_cubic(number: float) -> float:
    return 1.0 - pow(1.0 - number, 3.0)

func _get_arc_points() -> Array:
    var arc_points := []

    var start: Vector2 = current_card.global_position
    start.x += (current_card.size.x / 2)
    var target := get_local_mouse_position()
    var distance := (target - start)

    for i in ARC_POINTS:
        var t := (1.0 / ARC_POINTS) * i
        var r := _ease_out_cubic(t)
        var x := start.x + (distance.x / ARC_POINTS) * i
        var y := start.y + r * distance.y

        arc_points.append(Vector2(x, y))

    arc_points.append(target)


    return arc_points

func _on_card_aim_started(card_ui: CardUI) -> void:
    if not card_ui.card.is_single_target():
        return

    targeting = true

    area_2d.monitoring = true
    area_2d.monitorable = true

    current_card = card_ui

func _on_card_aim_ended(_card_ui: CardUI) -> void:
    targeting = false
    card_arc.clear_points()

    area_2d.position = Vector2.ZERO
    area_2d.monitoring = false
    area_2d.monitorable = false

    current_card = null

func _on_area_2d_area_entered(area: Area2D) -> void:
    print(current_card.targets)
    if not current_card or not targeting:
        return

    if not current_card.targets.has(area):
        current_card.targets.append(area)
    print(current_card.targets)

func _on_area_2d_area_exited(area: Area2D) -> void:
    if not current_card or not targeting:
        return

    if current_card.targets.has(area):
        current_card.targets.erase(area)
